---
title: 2018个税调整5000起征点计算方法-附excel公式
date: 2018-11-22 15:24:01
tags: 个税

---
2018年10月起国家提高了个人所得税的起征点，个税的计算方法也做了相应调整。 一位从事财务朋友想把Excel中的老个税公式换成调整后的税率公式。 自己摸索了半天也没能成功，就找我帮忙。

### 新老税率比较
下图可以比较清楚的看到，新老税率在各级应纳税额的税率情况。
以下是扣除5000起征点之后应纳所得税额。

![图1](./a.png)

看到新的个税的计算公式之后就开始动手做了，计算的逻辑并不复杂，只要将 (扣除五险一金后的工资 - 5000)  X 相应级数的税率即可。
譬如扣除五险一金后的工资是12000元，那个税的计算结果为700元。(12000 - 5000) X 10% = 700元。
但在验证的时候却发现了问题。网上找一个新个税计算的app进行测试， 发现个税的金额差了好多。还是拿刚刚例子中的数据12000元进行计算，发现只要交490元的个税。后来咨询了财务的朋友才知道可能和原来的速算扣除数有关。

### 速算扣除
原来一直不太清楚速算扣除的含义，一直以为仅仅是个税计算中认为设置的数值。曾经还怀疑会不会由于这个值设置的不够好，导致有的人涨工资后税后收入反而更少了。回到刚刚个税多算的问题，其实我的计算公式就有这个的问题。
譬如：当一个人当前扣除交金后的收入为8000元，应纳税额3000元，税率为3%（第一级税率）, 个税为90元，到手7910元。明年加了100元的工资，扣除交金后的收入为8100（不考虑交金），应纳税额3100元，税率10%（第二级税率），个税310元，到手7790元。税后收入反而比加工资之前更少了，显然不合理。
所以更合理的方式当然是原来应纳税额3000元依然按照3%的税率来算，仅对新加的100元收10%的个税。即100元 （90 + 10）。
合理的个税计算公式则是：应纳税额在各个级数下税额的汇总。
速算扣除也是按此思路扩展出来的
即：先根据级数计算应纳税额  - 当前税率下多算部分（速算扣除）
例子1  扣除交金后的收入为8100，应纳税额3100
个税税额 
=  3100 x 10% - 3000 x（10% - %3）
=  310 - 210 
= 100  ，210就是10%档的速算扣除数了
例子2  扣除交金后的收入为20000，应纳税额15000
个税税额 
=  15000 x 20% - 3000 x（20% - %3） - 9000（20% - %10）
 =  3000 - 510 - 900 
=  3000 - 1410
=  1590  ，1410 就是20%档的速算扣除数了
其它级数的速算扣除数

|级数	|级数条件	|税率	|速算扣除数|
| ------	| ------	| ------ | ------ |
| 1	|不超过3000元|	3%|	0|
| 2	| 超3000元--12000元| 10%|	210|
| 3	|超12000元--25000元| 20%|	1410|
| 4	|超25000元--35000元| 25%|	2660|
| 5	|超35000元--55000元| 30%|	4410|
| 6	|超55000元--80000元| 35%|	7160|
| 7	|超80000元 | 45%|	15160|

### excel公式编写
有了明确的个税计算逻辑之后，就着手开始编写excel中的公式了。
表格中和个税计算相关的列如下：

| 收入合计（扣金后收入）   |应纳税所得额(扣除5k)  |税率	|速算扣除数|应纳税额 |
| ------	| ------	| ------ | ------ |------ |  
| 20000 |15000| 20%| 1,410.00|1,590.00|

收入合计（扣金后收入）-B6：手动录入的值
应纳税所得额(扣除5k) - C6 ：=IF(B6<=5000,0,B6-5000)
税率 D6：=IF(C6=0,0,IF(C6<=3000,3%,IF(C6<=12000,10%,IF(C6<=25000,20%,IF(C6<=35000,25%,IF(C6<=55000,30%,IF(C6<=80000,35%,45%)))))))
速算扣除数- E6 ：=IF(C6<=3000,0,IF(C6<=12000,210,IF(C6<=25000,1410,IF(C6<=35000,2660,IF(C6<=55000,4410,IF(C6<=80000,7160,15160))))))
应纳税额 -F6：=ROUND(C6*D6-E6,2)

###### 直算法：

|  收入合计（扣金后收入）   |应纳税额|
| ------	| ------	|  
| 20000 |1590|

收入合计（扣金后收入）-B9：手动录入的值
应纳税额 -C9 ：=ROUND(MAX((B9-5000)*{3,10,20,25,30,35,45}%-5*{0,42,282,532,882,1432,3032},),2)
备注：这个是网上高人做的公式，把所有步骤化成1个公式，比较厉害。我的思路有些也是借鉴此公式而来。

### 最后
我非财务专业，所以很多术语可能都有问题, 如有意见或问题谢谢您帮忙指正。
最后附上两种算法的excel附件
附：2018个税计算方法_5000起征点